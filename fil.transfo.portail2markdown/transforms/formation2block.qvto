modeltype filMM "strict" uses "http://www.example.org/fIL";
modeltype markdownMM "strict" uses "http://www.example.org/simplemarkdown";


transformation formation2block(in fil : filMM, out md : markdownMM );

main() {
	fil.rootObjects().oclAsType(Faculte) -> map faculte2Title();
}

mapping filMM::Faculte::faculte2Title() : markdownMM::Document {
	name := self.nom;
	self.niveaux -> forEach(niveau){
		contents += niveau -> map niveau2Title();
		niveau.formations -> forEach(formation){
			contents += formation -> map formation2Title(); //affichage du nom de formation
			contents += formation.presentation -> map presentationParagraph(); //affichage du presentation du formation si elle existe
			contents += formation.s1 -> map s1FormationBlock(); //affichage du s1
			contents += object Title { level := 3; elements += object StrongEmphasis { text := "UES OBLIGATOIRES :"}};
			formation.s1.uesobligatoires -> forEach(ue){
				contents += ue -> map ueTitre();
				contents += ue.presentation -> map presentationParagraph();
			};
			contents += object Title { level := 3; elements += object StrongEmphasis { text := "UES OPTIONNELLES :"}};
			formation.s1.uesoptionnelles -> forEach(ue){
				contents += ue -> map ueTitre();
				contents += ue.presentation -> map presentationParagraph();
			};
			contents += formation.s2 -> map s2FormationBlock(); //affichage du s2
			contents += object Title { level := 3; elements += object StrongEmphasis { text := "UES OBLIGATOIRES :"}};
			formation.s2.uesobligatoires-> forEach(ue){
				contents += ue -> map ueTitre();
				contents += ue.presentation -> map presentationParagraph();
			};
			contents += object Title { level := 3; elements += object StrongEmphasis { text := "UES OPTIONNELLES :"}};
			formation.s2.uesoptionnelles-> forEach(ue){
				contents += ue -> map ueTitre();
				contents += ue.presentation -> map presentationParagraph();

			};
			contents += formation.responsables -> map responsablesParagraph("Responsables :");
			contents += formation -> map intervenantsParagraph();
		}
	}
}

/** Niveau **/
mapping filMM::Niveau::niveau2Title() : markdownMM::Title {
	level := 1;
	elements += self -> map niveau2Text();
}
mapping filMM::Niveau::niveau2Text() : markdownMM::Text {
	text := self.nom+' - '+self.pseudo;
}

/** Formation **/
mapping filMM::Formation::formation2Title() : markdownMM::Title {
	level := 2;
	elements += self -> map formation2Text();
}
mapping filMM::Formation::formation2Text() : markdownMM::Text {
	text := self.nom+' - '+self.pseudo;
}

/** Presentation**/
mapping filMM::Presentation::presentationParagraph() : markdownMM::Paragraph {
	elements += self -> map presentationText();
}
mapping filMM::Presentation::presentationText() : markdownMM::Text {
	text := self.contenue;
}

/** UE **/
mapping filMM::UE::ueTitre() : markdownMM::Title {
	level := 4;
	elements += self -> map ueTitreText();
}
mapping filMM::UE::ueTitreText() : markdownMM::Text {
	text := self.pseudo;
}
mapping filMM::UE::uePresentationParagraph() : markdownMM::Paragraph {
	elements += self.presentation -> map presentationText();
}

/** S1 **/
mapping filMM::S1::s1FormationBlock() : markdownMM::Title {
	level := 3;
	elements += self -> map s1FormationTitre();
}
mapping filMM::S1::s1FormationTitre() : markdownMM::Text {
	text := 'S1';
}

/** S2 **/
mapping filMM::S2::s2FormationBlock() : markdownMM::Title {
	level := 3;
	elements += self -> map s2FormationTitre();
}
mapping filMM::S2::s2FormationTitre() : markdownMM::Text {
	text := 'S2';
}

mapping filMM::Formation::responsablesParagraph(titre : String) : markdownMM::Paragraph
	when{self.responsables->notEmpty()}{
	elements += object StrongEmphasis { text := "Responsables :"};
	self.responsables -> forEach(int){
		elements += int -> map intervenantText();
	}	
}
mapping filMM::Formation::intervenantsParagraph() : markdownMM::Paragraph
	when{self.enseignants->notEmpty()}{
	elements += object StrongEmphasis { text := "Intervenants :"};
	self.enseignants -> forEach(int){
		elements += int -> map intervenantText();
	}
}
mapping filMM::Intervenant::intervenantText() : markdownMM::Text {
	text := self.nom +' '+self.prenom;
}
